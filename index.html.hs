{-# LANGUAGE OverloadedStrings #-}
-- vim: sw=2

import Prelude hiding (div, span)
import qualified Prelude as P
import Data.Monoid (mempty)
import qualified Data.Text as T

import Text.Blaze.Html5
import qualified Text.Blaze.Html5 as H
import Text.Blaze.Html5.Attributes hiding (label, form, span)
import qualified Text.Blaze.Html5.Attributes as A

import Text.Blaze.Html.Renderer.String (renderHtml)

import Angular

import System.IO.Unsafe (unsafePerformIO)


elem !. c = elem ! class_ c
elem !# i = elem ! A.id i

js uri = script ! src uri $ mempty
css uri = link ! rel "stylesheet" ! href uri

klass .$ innerHtml = H.div !. klass $ innerHtml
klass .! attr      = H.div !. klass ! attr

-- | Coerce that squirrely string literal
str :: String -> Html
str = toHtml

val :: String -> AttributeValue
val = toValue
-- -

-- For the 'modifiable' directive
modifiable = elemDirective "modifiable"
modData = customAttribute "mod-data"

main = putStrLn $ renderHtml $ do
  docType
  html ! lang "en" $ do
    H.head $ do
      H.title $ "Pose Teaching"
      meta ! httpEquiv "Content-Type" ! content "text/html; charset=utf-8"
      js "linkbot.js"
      js "bower_components/angular/angular.js"
      js "poseTeach.js"
      css "bower_components/bootstrap/dist/css/bootstrap.css"
    body ! ngApp "PoseTeaching" $ div ! ngController "actions" $ do
      adminSidebar
      programListingSection

adminSidebar =
  "sidebar" .$ do
    lllogo
    appTitle
    robotManager

lllogo =
  img !. "sidebar--logo"
      ! src "linkbot-labs-ER-logo-200x46px.png"

appTitle =
  h1 !. "sidebar--title" $ "Pose Teaching"

robotManager = do
  activeRobotDisplay
  robotForm $ do
    "form-group" .$ do
      roboInputLabel
      roboInput
    connectBtn
    roboIndicator

  where
  activeRobotDisplay = "active-robot" .! ngShow "m.robot !== null" $ do
    "{{m.robot._id}}"
    span !. "active-robot--status" $ "{{m.roboStatus}}"
  robotForm = form !. "sidebar--robot-mgr form-inline"
  roboInputLabel = label !. "sr-only" ! for "roboInput" $ "Linkbot ID"
  roboInput = input ! ngModel "m.robotIdInput" !. "form-control"
                    ! type_ "text"
                    ! placeholder "Linkbot ID"
                    ! ngDisabled "m.robot !== null"
  connectBtn = button !. "form-control" ! ngClick "connect()"
                      ! ngDisabled "m.robot !== null" $
                 "+"
  roboIndicator = "-"


programListingSection =
  section !. "program-listing container" $ do
    programControls
    programCode

programControls =
  "program-controls" .$ do
    button ! ngClick "runProgram()" $ "Run"
    a ! ngClick "clearProgram()" $ "Clear"

programCode =
  pre !. "program-code" $ do
    "program-code--boilerplate" .$
      pythonBoilerplate
    "program-code--code" .! ngRepeat "pose in m.poses" $ do
      codeLn ""
      codeLn "# Pose {{$index+1}}"
      codeLn $ do
        "linkbot.moveTo("
        modNum "pose[0]" >> ", "
        modNum "pose[1]" >> ", "
        modNum "pose[2]" >> ")"

pythonBoilerplate = mapM_ codeLn
  [ "#!/usr/bin/env python"
  , ""
  , "# This file generated by Linkbot Labs Pose Teaching"
  , ""
  , "import barobo"
  , "dongle = barobo.Dongle()"
  , "dongle.connect()"
  , "linkbot = dongle.getLinkbot('{{m.robot._id || 'ABCD' | uppercase}}')"
  , ""
  , do
    "linkbot.setJointSpeeds("
    modNum "m.speeds[0]" >> ", "
    modNum "m.speeds[1]" >> ", "
    modNum "m.speeds[2]" >> ")"
  ]

codeLn = (>> br)

modNum dataRef =
  modifiable ! customAttribute "number" "true"
             ! modData (val dataRef) $
      span ! A.style "border-bottom: 1px dotted black" $
        str $ template $ T.pack $ dataRef ++ " |number:1"
